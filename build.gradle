buildscript {
    apply from: 'versions.gradle'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.1.0'
    }
}

plugins {
    id 'idea'
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'com.gradle.plugin-publish' version '0.12.0'
    id 'de.fuerstenau.buildconfig' version '1.1.8'
    id 'com.github.blueboxware.tocme' version '1.1'
}

apply plugin: 'kotlin'
apply plugin: 'org.junit.platform.gradle.plugin'

group 'com.github.blueboxware'
version pluginVersion

repositories {
    mavenCentral()
    maven { url 'http://dl.bintray.com/jetbrains/spek' }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
    implementation "org.jetbrains.kotlin:kotlin-reflect"
    implementation "com.badlogicgames.gdx:gdx-tools:$gdxVersion"
    implementation "com.badlogicgames.gdx:gdx-backend-lwjgl:$gdxVersion"
    implementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    implementation "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-desktop"
    implementation 'commons-io:commons-io:2.6'
    implementation 'org.apache.commons:commons-text:1.2'

    testImplementation gradleTestKit()
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.jetbrains.spek:spek-api:1.1.5'
    testRuntimeOnly 'org.jetbrains.spek:spek-junit-platform-engine:1.1.5'
    // Needed for the Spek plugin to work correctly
    testImplementation "com.fasterxml.jackson.module:jackson-module-kotlin:2.9.4"
}

buildConfig {
    buildConfigField 'String', 'GDX_VERSION', gdxVersion
}

gradlePlugin {
    plugins {
        gdxPlugin {
            id = 'com.github.blueboxware.gdx'
            implementationClass = 'com.github.blueboxware.gdxplugin.GdxPlugin'
        }
    }
}

pluginBundle {
    website = 'https://github.com/BlueBoxWare/LibGDXGradlePlugin'
    vcsUrl = 'https://github.com/BlueBoxWare/LibGDXGradlePlugin.git'
    description = 'Plugin to create Texture Packs, Bitmap Fonts, Nine patches and Distance Fields for use with LibGDX'
    tags = ['LibGDX']

    plugins {
        gdxPlugin {
//            id = 'com.github.blueboxware.gdx'
            displayName = 'LibGDX Gradle plugin'
        }
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = jvmTarget
}
compileTestKotlin {
    kotlinOptions.jvmTarget = jvmTarget
}

junitPlatform {
    filters {
        engines {
            include 'spek'
        }
    }
}

tocme {
    docs("README.md.src")
}

task createReadmes {

    inputs.file("README.md.src")
    inputs.file("versions.gradle")

    outputs.files(fileTree(dir: '.', include: '*.md'))

    dependsOn(tasks.getByName("insertTocs"))

    doLast {

        String baseText = file("README.md.src")
                .text
                .replaceAll(~'(?s)<exclude>.*?</exclude>\\s?', '')
                .replaceAll(~'(?s)</?test[^>]*>\\s*', '')
                .replaceAll(~'<releasedVersion>', releasedPluginVersion)
                .replaceAll(~'<pluginVersion>', pluginVersion)
                .replaceAll(~'<gdxVersion>', gdxVersion)

        file("README.md").write(
                baseText
                .replaceAll(~'(?s)<kotlin>.*?</kotlin>', '')
                .replaceAll(~'(?s)```kotlin.*?```\\n?', '')
                .replaceAll(~'(?s)</?groovy>', '')
        )
        file("README-kotlin.md").write(
                baseText
                .replaceAll(~'(?s)<groovy>.*?</groovy>', '')
                .replaceAll(~'(?s)```groovy.*?```\\n?', '')
                .replaceAll(~'(?s)</?kotlin>', '')
        )
    }

}

